1 ? " *************************************"
2 ? " *                                   *"
3 ? " *     TRAVELLER TRADER 2020.04      *"
4 ? " *                                   *"
5 ? " *  - PILOT A MARAVA FREE TRADER     *"
6 ? " *  - BUY AND SELL CARGO             *"
7 ? " *  - EXPLORE THE SPINWARD MARCHES   *"
8 ? " *  - UPGRADE TO A NEW SHIP          *"
9 ? " *                                   *"
10 ? " *************************************"
11 ?
12 REM INITIALIZE GAME
13 GOSUB 148
14 F = FRE(1)
15 GOSUB 127
16 UL$="------------------------------"
17 MV%=0
18 ?:? "FREE MEMORY: " F "BYTES"
19 ?
20 REM MAIN LOOP
21 GOSUB 83
22 ? "MENU" : ? UL$
23 ? " (B)YE"
24 IF LEFT$(UW$,1) = "A" THEN ? " (L) SHIP CATALOG"
25 ? " (M)AP"
26 ? " (T)RADE"
27 ? " (####) JUMP TO HEX"
28 IN$ = "" : INPUT IN$
29 IF IN$ = "B" THEN GOTO 37
30 IF LEFT$(UW$,1) = "A" AND IN$ = "L" THEN GOSUB 89
31 IF IN$ = "M" THEN GOSUB 119
32 IF IN$ = "T" THEN GOSUB 130
33 IF IN$ = "" GOTO 21
34 IF VAL(IN$) > 0 THEN GOSUB 65
35 GOTO 21
36 REM BYE AND SHOW SCORE
37 ? " *************************************"
38 ? " *                                   *"
39 ? " *      THANK YOU FOR PLAYING        *"
40 ? " *        TRAVELLER TRADER!          *"
41 ? " *                                   *"
42 ? " *************************************"
43 ?:? "YOUR STATISTICS:" :? UL$
44 ? " - JUMPS:    " MV%
45 ? " - CREDITS:  " CR "
46 IF MV%>0 THEN ? " - CR/JUMP:  " INT(CR/MV%)
47 ?
48 ? "PROGRAM WRITTEN BY ROBERT EAGLESTONE."
49 ?
50 ? "THE TRAVELLER GAME IN ALL FORMS IS "
51 ? " - OWNED BY FAR FUTURE ENTERPRISES. "
52 ? " - COPYRIGHT 1977 - 2020 FAR FUTURE "
53 ? "   ENTERPRISES. "
54 ? " - A REGISTERED TRADEMARK OF FAR"
55 ? "   FUTURE ENTERPRISES. "
56 ? " THIS PROGRAM IS FOR PERSONAL,"
57 ? "   NON-COMMERCIAL USE ONLY. "
58 ? " ANY USE OF FAR FUTURE ENTERPRISES"
59 ? "   COPYRIGHTED MATERIAL OR TRADEMARKS "
60 ? "   ANYWHERE IN THIS PROGRAM SHOULD NOT"
61 ? "   BE VIEWED AS A CHALLENGE TO THOSE"
62 ? "   COPYRIGHTS OR TRADEMARKS."
63 END
64 REM DO A JUMP
65 ?:? "COMPUTING JUMP TO HEX " IN$
66 C1 = VAL(LEFT$(IN$,2))       :REM DESTINATION COL
67 R1 = VAL(RIGHT$(IN$,2))      :REM DESTINATION ROW
68 JR  = SH(0,6)                :REM SHIP JUMP RATING
69 GOSUB 558    :REM POPULATES D% = DISTANCE
70 GOSUB 591 :REM OK GO
71 GOSUB 497
72 SY$=SY$(C,R)
73 GOSUB 520
74 FOR TT=0 TO 2000:NEXT
75 ?:? " - ENGAGING JUMP DRIVE."
76 FOR TT=0 TO 1000:NEXT
77 ?:? " - IN JUMPSPACE. PLEASE STAND BY..."
78 F = FRE(1)
79 IF IN$ = LO$ THEN MV%=MV%+1:?:? "WELCOME TO " NA$ : GOSUB 127
80 IF IN$ <> LO$ THEN ?:? "JUMP FAILURE"
81 RETURN
82 REM CURRENT STATUS
83 SY$ = SY$(C,R)
84 ?:? "BALANCE: CR" CR
85 GOSUB 520
86 ?:? "(" NA$ " " UW$ ") "; 
87 RETURN
88 REM SHOW SHIP CATALOG
89 ?
90 ? "     QSP    CLASS       CARGO  MCR"
91 ? "----------------------------------"
92 BS%=0
93 FOR N=0 TO 20
94 QS$=QS$(N) :IF QS$ = "" GOTO 105
95 PR%=QC%(N)
96 IF INT(CR/1000000) > PR% THEN BS% = 1
97 Q1$=LEFT$(QS$,6)  :REM QSP
98 Q3$=MID$(QS$,8,3) :REM CARGO
99 Q4$=MID$(QS$,12)  :REM CLASS
100 Q4$ = LEFT$(Q4$+"          ",12)
101 ? N; :IF N<10 THEN ? " ";
102 ? " " Q1$ " " Q4$ " " Q3$ "T " PR%
103 ?
104 NEXT
105 IF BS%=0 GOTO 117
106 INPUT "# TO BUY A SHIP (INVALID NUMBER QUITS)"; N
107 IF N > 20 OR N < 0 OR QS$(N) = "" GOTO 117
108 QS$=QS$(N)
109 PR%=QC%(N)
110 ? "DONE!  PRICE=" PR% * 1000000
111 CR = CR - (PR% * 1000000)
112 ? "BALANCE: " CR
113 SH(0,4) = VAL(MID$(QS$,5,1))
114 SH(0,6) = VAL(MID$(QS$,6,1))
115 SH(0,9) = VAL(MID$(QS$,8,3))
116 SH% = N
117 RETURN
118 REM SHOW JUMP MAP
119 ?:? "JUMP RANGE MAP:":? UL$
120 JR=SH(0,6):GOSUB 568
121 FOR N=0 TO 24
122 IF MA$(N) <> "" THEN ? MA$(N)
123 NEXT
124 ?
125 RETURN
126 REM CALC FLUX
127 FL = INT((RND(1)-RND(1))*6)
128 RETURN
129 REM TRADE
130 GOSUB 597
131 ?:INPUT "SELL CARGO TYPE (0-10 OR Q)"; S$
132 IF S$ = "Q" GOTO 139
133 S = VAL(S$) : IF S > 10 GOTO 139
134 ? "SELLING " MID$(CT$(S),4,12)
135 INPUT "HOW MANY TONS (0 TO QUIT)    "; T$
136 T = VAL(T$) : IF T = 0 OR T > CA(S) GOTO 139
137 CA(S) = CA(S) - T
138 CR = CR + CP(S) * T
139 ?:INPUT "BUY CARGO TYPE (0-10 OR Q)"; B$
140 IF B$ = "Q" THEN RETURN
141 B = VAL(B$) : IF B > 10 THEN RETURN
142 ? "BUYING " MID$(CT$(B),4,12)
143 INPUT "HOW MANY TONS (0 TO QUIT)    "; T$
144 T = VAL(T$) : IF T = 0 THEN RETURN
145 CA(B) = CA(B) + T
146 CR = CR - CP(B) * T
147 RETURN
148 ? "INITIALIZING:"
149 REM
150 REM  INITIALIZE TRANSLATION ALPHABETS
151 REM
152 AL$(0) = " ABCDEFGHIJKLMNOPQRSTUVWXYZ?:'-"
153 AL$(1) = " 0123456789ABCDEFGHILMNORSTUWY-"
154 ? " - JUMP RANGE MAP"
155 DIM MA$(24)
156 C = 19
157 R = 10
158 SH(0,4) = 1  :REM MANEUVER
159 SH(0,6) = 2  :REM JUMP 
160 SH(0,9) = 60 :REM CARGO
161 SH% = 1      :REM MARAVA
162 REM READ SHIP QSPS
163 DIM QS$(20) : REM QSPS
164 DIM QC%(20) : REM MCR
165 N = 0
166 ? " - SHIP CATALOG"
167 READ Q$: IF Q$ = "0" GOTO 181
168 QS$(N) = Q$ 
169 REM CALCULATE COST
170 Q7% = VAL(MID$(Q$,7,1))
171 HU% = ASC(MID$(Q$,3,1))-64
172 IF HU% > 14 THEN HU% = HU% - 1 :REM O
173 IF HU% > 8  THEN HU% = HU% - 1 :REM I
174 PR% = 8 * HU%
175 PR% = PR% + HU% * VAL(MID$(Q$,5,1)) :REM M
176 PR% = PR% + HU% * VAL(MID$(Q$,6,1)) :REM J
177 PR% = PR% * Q7%
178 QC%(N) = PR%
179 N=N+1
180 GOTO 167
181 REM
182 REM READ CARGO TYPES
183 REM
184 REM A ROCK-SCISSORS-PAPER CIRCULAR
185 REM RELATIONSHIP.
186 REM
187 REM   1-2: PRODUCER WORLD
188 REM  4-15: CARGO NAME
189 REM    16: BASE PRICE (KCR)
190 REM    17: PRICE MARGIN (MULTIPLE)
191 REM 18-19: CONSUMER WORLD
192 REM
193 DIM CT$(10)
194 ? " - CARGO TYPES"
195 CT$(0)  = "AG TISANES     87RI"
196 CT$(1)  = "AS MINERALS    36NI"
197 CT$(2)  = "BA GEMS        75VA"
198 CT$(3)  = "DE SEEDSTOCK   68AG"
199 CT$(4)  = "FL EXOTICS     22LO"
200 CT$(5)  = "HI CHELATES    52AS"
201 CT$(6)  = "LO FLORA       24BA"
202 CT$(7)  = "NI RARE EARTHS 93HI"
203 CT$(8)  = "PO ARTWORK     49DE"
204 CT$(9)  = "RI BRANDS      11PO"
205 CT$(10) = "VA CRYSTALS    32FL"
206 REM
207 REM READ SHIP CARGO
208 REM
209 REM FORMAT: TONS, TL, TC INDEX
210 REM
211 DIM CA(10)
212 DIM CP(10)
213 ? " - SHIP CARGO"
214 CA(0)  = 5
215 CA(1)  = 5
216 CA(2)  = 5
217 CA(3)  = 5
218 CA(4)  = 5
219 CA(5)  = 5
220 CA(6)  = 5
221 CA(7)  = 5
222 CA(8)  = 5
223 CA(9)  = 5
224 CA(10) = 5
225 REM
226 REM READ TRADE CODE INDEX
227 REM THE MOST COMMON TC MIXES.
228 REM
229 ? " - TRADE CODES"
230 DIM TC$(50)
231 X=0
232 READ TC$ : IF TC$ = "0" GOTO 236
233 TC$(X)=TC$
234 X=X+1
235 GOTO 232
236 REM =============================
237 REM 
238 REM  READ SYSTEM DATA
239 REM 
240 REM =============================
241 DIM SY$(32,40)
242 ? " - SECTOR DATA"
243 READ SY$:IF SY$ = "0" GOTO 249
244 LO$ = LEFT$(SY$,4)
245 C0 = VAL(LEFT$(LO$,2))
246 R0 = VAL(RIGHT$(LO$,2))
247 SY$(C0,R0) = MID$(SY$,5)
248 GOTO 243
249 REM DONE
250 ?:? "DATA LOAD COMPLETE." 
251 RETURN
252 REM -------------------------------------------
253 REM QSP + STRENGTH + CARGO + ALIGNMENT + NAME
254 REM -------------------------------------------
255 DATA S-AL222  3IMURPHY
256 DATA S-AA223  5ISERPENT
257 DATA A-BS111 80 BEOWULF
258 DATA A-BS121 60 MARAVA
259 DATA P-DA643 40*AZ ALRRAK
260 DATA R-DA111200 MARCH
261 DATA G-DL713 10AKHOSAA
262 DATA E-DU523  2IGAZELLE
263 DATA M-FU131120 BRILLIANCE
264 DATA E-FS413 24ZSESAVETL
265 DATA G-FU423  3ZSHIVVA
266 DATA C-HU333 85 BROADSWORD
267 DATA F-KS131400 SUSA
268 DATA R-TB432250 LEVIATHAN
269 DATA 0
270 REM
271 REM  TRADE CODE INDEX TABLE
272 REM
273 DATA ""
274 DATA AG
275 DATA AG NI
276 DATA AG NI RI
277 DATA AG RI
278 DATA AS BA VA
279 DATA AS HI IN VA
280 DATA AS LO VA
281 DATA AS NI VA
282 DATA AS VA
283 DATA BA
284 DATA BA DE PO
285 DATA BA FL
286 DATA BA PO
287 DATA BA VA
288 DATA DE HI IN PO
289 DATA DE HI PO
290 DATA DE LO PO
291 DATA DE NI
292 DATA DE NI PO
293 DATA DE NI RI
294 DATA DE PO
295 DATA DE RI
296 DATA FL
297 DATA FL HI IN
298 DATA FL LO
299 DATA FL NI
300 DATA HI
301 DATA HI IN
302 DATA HI IN PO
303 DATA HI IN VA
304 DATA HI PO
305 DATA LO
306 DATA LO PO
307 DATA LO VA
308 DATA NI
309 DATA NI PO
310 DATA NI RI
311 DATA NI VA
312 DATA PO
313 DATA RI
314 DATA VA
315 DATA 0
316 REM --------------------------------------------
317 REM
318 REM DATA IS IN THIS FORMAT:
319 REM
320 REM    1-4: HEX LOC
321 REM      5: STARPORT + BASES
322 REM    6-7: TRADE CODES INDEX
323 REM      8: TL
324 REM      9: TC1 AN,CP,RE,RS,SA
325 REM     10: BELT, GG, AND ZONE INDICATORS
326 REM     11: ALLEGIANCE
327 REM    12+: NAME
328 REM
329 REM ---------------------------------------------
330 DATA "0101>199;>ZZEYCUDE", "0102>21A;AZRENO", "0103U37B;<ZERRERE"
331 DATA "0104>329;<ZCANTREL", "0108>238;>NGYOMAR", "0202>2 5;=ZTHENGO"
332 DATA "0301>3 8;=NRIO", "0303U 0C;AZGESENTOWN", "0304T27D=<ZCHRONOR"
333 DATA "0307U39A<@ZATSA", "0503?2 8;<NWHENGE", "0601@1 6;>NENLAS-DU"
334 DATA "0605>3 9;<NALGEBASTER", "0607@357;<NRASATT", "0608T 0C;<ZNINJAR"
335 DATA "0610U1 A;>ZSHEYOU", "0703@356;<NINDO", "0704@357;<NNEREWHON"
336 DATA "0705T40C;>ZCIPANGO", "0710@4 2;=NSTAVE", "0805G357;ACNARVAL"
337 DATA "0807@325;<NPLAVEN", "0808M39B;ACQUAR", "0810@259;=CFROND"
338 DATA "0901@268;>NCONDYOLE", "0902>259;=NPUPARKIN", "0904U4 A;>ZCHWISTYOCH"
339 DATA "0909>35A;@CGOUGESTE", "1004>3 8;BNESALIN", "1005E38B;=IRUBY"
340 DATA "1006E2 B;>IEMERALD", "1010?197;>NZENOPIT", "1102>279;BZRIVERLAND"
341 DATA "1103U 0A;AZCLAN", "1106T28C;>IJEWELL", "1110F358;>NZIRCON"
342 DATA "1201@357;>ZAO-DAI", "1204T3 A;=IMONGO", "1209F357;<NUTOLAND"
343 DATA "1210@3 4;@NPEQUAN", "1305?325;AINAKEGE", "1307E35A;>ILYSEN"
344 DATA "1401=358;@ZFOELEN", "1402T38B;BZFARREACH", "1510@130;=N871-438"
345 DATA "1604?298;<ILOUZY", "1607;323?FIGRANT", "1705<28D<;IEFATE"
346 DATA "1706=40A;@IALELL", "1802=239;>IYRES", "1803>317;<IMENORB"
347 DATA "1805=35D;<IUAKYE", "1806@327;>IWHANGA", "1807@4 2@>IKNORBES"
348 DATA "1808?355;>IFORBOLDN", "1809>287;ANRUIE", "1810F359;>IJENGHE"
349 DATA "1903L34D<=IPIXIE", "1904D26D<=IBOUGHENE", "1909F387;<IHEFRY"
350 DATA "1910T40CN=IREGINA", "2005E40B;<IFERI", "2007F287;BIROUP"
351 DATA "2106;352;EIPSCIAS", "2110>22AD>IYORI", "2201F35A;<IDENTUS"
352 DATA "2202<378;>IKINORB", "2204?354<AIBECK'S WORLD", "2205> 07;;IENOPE"
353 DATA "2207@239;AIWOCHIERS", "2303>269;<IYORBUND", "2306>9 8<FISHIONTHY"
354 DATA "2308;274;FIALGINE", "2309@268;>IYURST", "2402=4 5;>IHEYA"
355 DATA "2405@ 05;BIKENG", "2406>35B;=IMOUGHAS", "2408@168;>IRETHE"
356 DATA "2410U1 9<>IINTHE", "2509L329;=IPAYA", "2510>268;AIDHIAN"
357 DATA "2602>2 8<BICORFU", "2607@2 7;>IFOCALINE", "2701=2 A;=ILABLON"
358 DATA "2706@329;<IHEGUZ", "2708>35A;<IVIOLANTE", "2905@100;>IPAVANNE"
359 DATA "2906>35B;=ICARSTEN", "2908@1 7;AIZILA", "3001>327;>IJESEDIPERE"
360 DATA "3002>238;>IYEBAB", "3003E35B;>INASEMIN", "3004?2 6;@IZYKOCA"
361 DATA "3005=277;@IARAMANX", "3008>2 5;AIPYSADI", "3010M35B;=IL'OEUL D'DIEU"
362 DATA "3102=26A;BIRUGBIRD", "3103E35A;BITOWERS", "3104>33C;<IFENETEMAN"
363 DATA "3107?357;EILEWIS", "3110T35B=<IARAMIS", "3202=27B;@IJUNIDY"
364 DATA "3207>8 9<>IPATINIR", "3209M328;=INATOKO", "3210>268;BIREACHER"
365 DATA "0111=358;>NATSON", "0114>26A;>NYIKTOR", "0115@325;<NXHOSA"
366 DATA "0212>356;<NPRINX", "0215@3 4;=CRUSHU", "0218@348;>NBAEL"
367 DATA "0311=358;>NMIZAN-FEL", "0412U34A;=ZSANSIBAR", "0414>388;<NATTICA"
368 DATA "0416@249C@CRETINAE", "0511>329;>ZTERRA NOVA", "0512@355;=ZASMODEUS"
369 DATA "0518?355;<CFAISAL", "0613=35C;=ZLEBEAU", "0614U1 9==ZQUERION"
370 DATA "0618@406;<CDEKALB", "0620@2 6;=SWINSTON", "0712F328;=ZRAPP'S WORLD"
371 DATA "0717=36C;<CTHANBER", "0720@27B;@SENTROPE", "0820>358;=SANSELHOME"
372 DATA "0911>1 5;<NCALORAN", "0912@ 08;<N899-076", "0915=389;=CQUARE"
373 DATA "0919;100?DNZETA 2", "1011=279;@NARDEN", "1018>349;=ICHOLEOSTI"
374 DATA "1020>2 6;@IMARGESI", "1116L38A=<IFRENZIE", "1118E 0A;>IGARDA-VILIS"
375 DATA "1119<28A;<IVILIS", "1212@356;<NDIGITIS", "1213@347;ACEDININA"
376 DATA "1214@100;<I728-907", "1216?268;<ISTELLATIO", "1217@ 06;=IARKADIA"
377 DATA "1311= 0C;=NTREMOUS DEX", "1315P328;<IMIRRIAM", "1320?2 7K<ISAURUS"
378 DATA "1411@357;=NRANGENT", "1413M35A;>IDENOTAM", "1417@325;<IFICANT"
379 DATA "1511>328;@CTIONALE", "1515> 07;=ICALIT", "1519;395;DIASGARD"
380 DATA "1520@100;>ITAVONNI", "1611>3 8;<IPHLUME", "1711M 0A;<IEXTOLAY"
381 DATA "1719T35B=<ILANTH", "1811?38AK=IDINOM", "1815M 0A;=IGHANDI"
382 DATA "1817? 02<FIVICTORIA", "1912E2 9;=IDINOMN", "1916;2 4;FIYLAVEN"
383 DATA "1918?357;BISONTHERT", "1920M19D;>ID'GANZIO", "2011@269;BIWYPOC"
384 DATA "2111;100;FIDJINNI", "2112?1 6;AIRECH", "2212>258;=IK'KIRKA"
385 DATA "2215=36A;BIQUOPIST", "2311?398;<ITREECE", "2313>32A;<IECHISTE"
386 DATA "2314?325;=IPIREMA", "2317>38A;@IRHISE", "2319U35A;>IIVENDO"
387 DATA "2411F327;>IKEANOU", "2414>2 9;>ITUREDED", "2415@353C=IVREIBEFGER"
388 DATA "2416>324;=ILA'BELLE", "2417E 0B;=IEQUUS", "2418M357;=IICETINA"
389 DATA "2419>379;>ICOGRI", "2420N399;=ISKULL", "2512>359;=IKINORB"
390 DATA "2514>326;=IGILEDEN", "2519@269;>IPANNET", "2520E367;<IGARRINCSKI"
391 DATA "2612M8 E;=IMACENE", "2613<32D<<IFULACIN", "2620>279<AINATOKO"
392 DATA "2712L35A;=IRISEK", "2715<27B;=IPOROZLO", "2716T27F=<IRHYLANOR"
393 DATA "2720>327;=ILONESEDA", "2811@355;>IVALHALLA", "2812>24B;BIZIVIJE"
394 DATA "2814L188;>IJAE TELLONA", "2818;100;EIGEROME", "2912<2 B;>IHENOZ"
395 DATA "2913U359;=ICELEPINA", "2918=8 9;<IGITOSY", "3015=2 5;>IBELIZO"
396 DATA "3016@353;BIKEGENA", "3017@358;<IHERONI", "3019; 05;FI457-973"
397 DATA "3020> 0B;=ISOMEM", "3111=359;<IVINORIAN", "3112M328;>INUTEMA"
398 DATA "3114;100;DIHUDERU", "3118=1 6;>ICIPATWE", "3119>405C<IVANEJEN"
399 DATA "3212T32C;<IMARGESI", "3216G15A;BIBEVEY", "3218>19B;=ITACAXEB"
400 DATA "3220>2 5;>IPOWAZA", "0122?194;<NJUNCTION", "0129@357;<NUNIQUA"
401 DATA "0130<41A;@NGAROO", "0223=36B;=DSTERN-STERN", "0230@130;=N886-945"
402 DATA "0321F39A;>NNONYM", "0325E357;>DLABERV", "0326>369;>DEKTRON"
403 DATA "0421>28A;>DZAMINE", "0425>1 8;=DENGRANGE", "0426U 09;=DILIUM"
404 DATA "0427=4 9;<DROGET", "0429@336<<NKARDIN", "0430>2 5;<CBULARIA"
405 DATA "0526?3 3;=DRORRE", "0527T27C;<DMIRE", "0528@1 5;ANCONDARIA"
406 DATA "0530@368;<NDORANNIA", "0622?199;>DTERANT 340", "0624<39D;<DJACENT"
407 DATA "0625;100?DD494-908", "0627<27G<>DDARRIAN", "0721;336;DDTORMENT"
408 DATA "0723>2 9;<DTRIFUGE", "0724=36C;@DNOSEA", "0727F17A;>DSPUME"
409 DATA "0729?327<>NATOR", "0822@2 3;=DCUNNONIC", "0830=329;>CDEBARRE"
410 DATA "0921U409;>SHRUNTING", "0922U40A;>STIZON", "0927U28A;>SNARSIL"
411 DATA "0930<36B;<IFLAMMARION", "1022U3 B;>SCOLADA", "1026U40B;>SANDURIL"
412 DATA "1121U19A;>SMJOLNIR", "1123U4 A;ASJOYEUSE", "1126U23A;=SORCRIST"
413 DATA "1130H194;@SENOS", "1221E1 8;>SGUNGNIR", "1223T28C;=SGRAM"
414 DATA "1225U 0A;=SEXCALIBUR", "1324U 0A;=STYRFING", "1325T28C;=SSACNOTH"
415 DATA "1329E4 A;<ICALADBOLG", "1424U3 A;<SBEATER", "1429@328;AIGUNN"
416 DATA "1430@8 A;>ICALIBURN", "1522U 0A;=SDYRNWYN", "1523U4 B;>SDURENDAL"
417 DATA "1524U40A;=SHOFUD", "1525U1 A;=SSTING", "1526U2 A;=SBITER"
418 DATA "1529@100?>SSTEEL", "1626@100?>SIRON", "1627@ 00?<SBRONZE"
419 DATA "1628@100?=SMITHRIL", "1721>34C;<IARBA", "1727E35B;=IWARDN"
420 DATA "1728>327;<IOLYMPIA", "1729>219;=ISMOUG", "1822G 06;BIRABWHAR"
421 DATA "1824L 0B;AIADABICCI", "1825=8 B;=IZAIBON", "1826<1 E<<ITENALPHI"
422 DATA "1924@205;>IIANIC", "1927F408;>ISPIRELLE", "2024F 08;=IDERCHON"
423 DATA "2124T28D=<ILUNION", "2125E2 B;BISHIRENE", "2128?325;@IPENKWHAR"
424 DATA "2129>219;<IHARVOSETTE", "2224>329;=ICARSE", "2228= 0A;>IPERSEPHONE"
425 DATA "2321=328;>IQUIRU", "2322;322;EIGORRAM", "2323E32B;=IRESTEN"
426 DATA "2324M1 A;<ICAPON", "2325>32A;=ISHARRIP", "2327L28D;<ISTROUDEN"
427 DATA "2425@7 8;=IGANDR", "2426@258;=IDROLRAW", "2521=288;BIHERONI"
428 DATA "2523=3 6;>IBYRET", "2527@347;=IPIMANE", "2621<36A;<IFOSEY"
429 DATA "2624U2 8;=IMERCURY", "2627>358;=ITIVID", "2726>329;<ICAREY"
430 DATA "2728<39BCAIDUALE", "2824>199;@ICATUZ", "2827F325;>IMELETO"
431 DATA "2828M358;>IHEXOS", "2830F 07;=IPEDASE", "2924N328;=IMORAN"
432 DATA "2927< 0B;>IMAITZ", "2930F33A;=IMAINZ", "3021>357;>IBRODIE"
433 DATA "3022>32A;=IRORISE", "3024= 07;@IJOKOTRE", "3025<27C;=IFORNICE"
434 DATA "3026@327;=IGRILLE", "3029< 0E;@IPALIQUE", "3030F358;=INEXINE"
435 DATA "3123G177;<INADRIN", "3124T28F;>IMORA", "3223F 07;<IDOJODO"
436 DATA "3228>329;BIFENL'S GREN", "0133M2 B;AIEMAPE", "0139M17B;@IRAWEH"
437 DATA "0140@323;=N876-574", "0231@268;<NSAXE", "0236>1 9<EIANDOR"
438 DATA "0240@358;>N769-422", "0332@367;@IGOTHE", "0333= 0A;BIMIRRIAM"
439 DATA "0336>358<DICANDORY", "0340@354;<NWONDERAY", "0433M 09;@IJONE"
440 DATA "0440?347;ANJINX", "0532?2 7;<CUCELLA", "0533>204;>IPENELOPE"
441 DATA "0534T4 C;@IKARIN", "0538M1 7;@IWONSTAR", "0539>329C=IFROIN"
442 DATA "0632F1 7;>IGOHATURE", "0637> 09;@IQUHAIATHAT", "0638@357;AILAKOU"
443 DATA "0731@358;=NRALHE", "0732L4 C==IIDERATI", "0739@ 07;=NTONDOUL"
444 DATA "0834H357;@C875-496", "0837@2 7;<NOCHECATE", "0838?4 5;=NMEWEY"
445 DATA "0840@349;>N975-452", "0931=26A;<NASTELTINE", "0938?17A;>NINCHIN"
446 DATA "0940?396;=NSINGER", "1031@130;<N567-908", "1037>36A;>NAVASTAN"
447 DATA "1040> 0A;<NKUAI QING", "1131@355;<NFALDOR", "1132G7 9;>CBOWMAN"
448 DATA "1133>359;<NSQUALLIA", "1138=3 A;=CTARSUS", "1232F328;=CWALSTON"
449 DATA "1233@268;<NFLEXOS", "1237E28D;=CCOLLACE", "1238>406;ANPAVABID"
450 DATA "1331@358;=NDATRILLIAN", "1332;140;FNNIRTON", "1337@120C>CJUDICE"
451 DATA "1339=40C;>NTREXALON", "1340M3 5;<CMOTMOS", "1433@238;=NNOCTOCOL"
452 DATA "1434F3 7;@CTARKINE", "1435=409;<CDALLIA", "1436@369;<NTALOS"
453 DATA "1531@100;>NDAWNWORLD", "1532M358;=CELIXABETH", "1533? 0A;<NFORINE"
454 DATA "1537E40B;<IMERTACTOR", "1631>268;ACTALCHEK", "1632@ 07;<NMILAGRO"
455 DATA "1634>404;>NPAGATON", "1635<34A;<NBINGES", "1637U26C;=IMILLE FALCS"
456 DATA "1731<38B;>IGROTE", "1733@357;=ILYDIA", "1736?197;BIMELIOR"
457 DATA "1737M269;>IEGYPT", "1739>359;=IASTER", "1836@216;<ICALLIA"
458 DATA "1932>268;=IMITHRAS", "1934<35B;AIWEISS", "1935>359;<IWINDSOR"
459 DATA "1937=2 9;>IOVERNALE", "1938M 0B;=INEW ROME", "1939>355;>ICRAW"
460 DATA "2035=299;BIAKI", "2036T6 F;>IGLISTEN", "2038>35B<=ITRANE"
461 DATA "2132@367;<ICENTRY", "2134>365;<ICALEDONIA", "2137@3 2;>ISOREL"
462 DATA "2138F 0A;<IHOROSHO", "2140U198;>IROMAR", "2231G4 5?BIMARASTAN"
463 DATA "2232@277;BICROUT", "2233>24B;>ITIREM", "2234>38B;>IINTHE"
464 DATA "2236?197;=ITSARINA", "2237F32A;@IWURZBURG", "2331@352;<IBICORNN"
465 DATA "2334< 0C;AIFFUDN", "2336T2 C;<IBENDOR", "2534>378;=IBURTSON"
466 DATA "2536<38B;=ISQUANINE", "2537D19A;>IDOBHAM", "2538@322;>IPYRAMUS"
467 DATA "2539@196;BITHISBE", "2540= 06;>IARAMIS", "2637>8 C;AIROBIN"
468 DATA "2638@ 05;<ID'MARA", "2639>359;=IKELTCHNER", "2731=327;<ITUSSINIAN"
469 DATA "2733<40B;BIEDENELT", "2735G2 7;>ICONWAY", "2739F297;BIDODDS"
470 DATA "2832@325;=ILEANDER", "2833?2 3;=IPEPERNIUM", "2834=186<<ITRALTHA"
471 DATA "2839F357;=IFARQUAHAR", "2933@2 6;=IRAYDRAD", "2934=357;=IZYRA"
472 DATA "2935M356;=IMURCHISON", "2936<36B;BIHAMMERMIUM"
473 DATA "2940G358;=ITHORNNASTOR"
474 DATA "3032=36B;=IKATARULU", "3035=2 6;<IPRILISSA", "3038>359;=ITEE-TEE-TEE"
475 DATA "3039<32B;=IYOUGHAL", "3040G359;=ITENELPHI", "3138>2 5;AIZEPHYR"
476 DATA "3139E2 5;>ICHAMOIS", "3233= 08;>IRAMIVA", "3235T28F==ITRIN"
477 DATA "3236>1 5;@IHAZEL"
478 DATA 0
479 REM  CARGO PRICE PER TON
480 REM  (CA,UW$,TC$,FLUX) => PR
481 TL$ = RIGHT$(UW$,1)
482 TL = ASC(TL$)-48
483 IF TL > 16 THEN TL = TL - 7
484 PR = 100 + TL * 10
485 CT$ = CT$(CA)
486 PR = PR * VAL(MID$(CT$,16,1)) + FL * 10
487 IF LEFT$(CT$,2) = LEFT$(TC$,2) THEN PR = PR - 100
488 IF RIGHT$(CT$,2) = LEFT$(TC$,2) THEN PR = PR + 100 * VAL(MID$(CT$,17,1))
489 IF PR < 1 THEN PR = 10 :REM STOPGAP
490 RETURN
491 REM COMPOSE_HEX(C,R) -> C$,R$,LO$
492 C$ = "0" + MID$(STR$(C),2)
493 R$ = MID$(STR$(R),2)
494 IF R < 10 THEN R$ = "0" + R$
495 LO$ = C$ + R$
496 RETURN
497 REM COMPUTE-LOC(C,R) -> C$,R$,LO$
498 C$ = MID$(STR$(C),2)
499 IF C < 10 THEN C$ = "0" + C$
500 R$ = MID$(STR$(R),2)
501 IF R < 10 THEN R$ = "0" + R$
502 LO$ = C$ + R$
503 RETURN
504 REM IN$ HAS "ORDERED" UPP
505 REM UP$ HAS CANONICAL UPP
506 FOR X = 1 TO 6
507 VA = 10 - X
508 IF MID$(IN$,X,1) = "S" THEN UU(1) = VA
509 IF MID$(IN$,X,1) = "D" THEN UU(2) = VA
510 IF MID$(IN$,X,1) = "E" THEN UU(3) = VA
511 IF MID$(IN$,X,1) = "I" THEN UU(4) = VA
512 IF MID$(IN$,X,1) = "U" THEN UU(5) = VA
513 IF MID$(IN$,X,1) = "C" THEN UU(6) = VA
514 NEXT
515 UP$ = ""
516 FOR X = 1 TO 6
517 UP$ = UP$ + MID$(STR$(UU(X)),2)
518 NEXT
519 RETURN
520 REM
521 REM  DECODE SY$ INTO ITS COMPONENT PARTS:
522 REM
523 REM  D6323312TTGNEAEGCOR
524 REM      1: STARPORT + BASES
525 REM    2-3: TC INDEX
526 REM      4: TL
527 REM      5: TC1 AN,CP,RE,RS,SA
528 REM      6: BELT, GG, AND ZONE INDICATORS
529 REM      7: ALLEGIANCE
530 REM     8+: NAME
531 REM
532 SP=ASC(LEFT$(SY$,1))-59
533 BA$=" "
534 IF SP AND 8  =  8 THEN BA$ = "S"
535 IF SP AND 16 = 16 THEN BA$ = "N"
536 IF SP AND 24 = 24 THEN BA$ = "2"
537 SP = SP AND 7
538 SP$ = CHR$(64+SP)
539 IF SP = 0 THEN SP$ = "X"
540 UW$ = SP$ + "-" + MID$(SY$,4,1)
541 ZO = ASC(MID$(SY$,6,1))-59
542 BE = 0: GG = 0: ZO$ = " "
543 IF ZO AND 1 = 1 THEN BE = 1
544 IF ZO AND 2 = 2 THEN GG = 1
545 IF ZO AND 4 = 4 THEN ZO$ = "A"
546 IF ZO AND 8 = 8 THEN ZO$ = "R"
547 AL$ = MID$(SY$,7,1)
548 TX = VAL(MID$(SY$,2,2))
549 TC$ = TC$(TX)
550 T1 = ASC(MID$(SY$,5,1))-59
551 IF (T1 AND 1)  = 1  THEN TC$ = TC$ + " AN"
552 IF (T1 AND 2)  = 2  THEN TC$ = TC$ + " CP"
553 IF (T1 AND 4)  = 4  THEN TC$ = TC$ + " RE"
554 IF (T1 AND 8)  = 8  THEN TC$ = TC$ + " RS"
555 IF (T1 AND 16) = 16 THEN TC$ = TC$ + " SA"
556 NA$ = MID$(SY$,8)
557 RETURN
558 REM DISTANCE(C,R,C1,R1) -> D%
559 A1 = R + INT(C/2)
560 A2 = R1 + INT(C1/2)
561 D1% = ABS(A1-A2)
562 D2% = ABS(C-C1)
563 D3% = ABS( (A1-C) - (A2-C1) )
564 D% = D3%
565 IF (D1% >= D2%) AND (D1% >= D3%) THEN D% = D1%
566 IF (D2% >= D1%) AND (D2% >= D3%) THEN D% = D2%
567 RETURN
568 REM JUMP-MAP(C,R,JR) -> MA$()
569 FOR NN=0 TO 24 : MA$(NN) = "" : NEXT
570 NN%=0
571 FOR C1 = C-JR TO C+JR : IF C1 < 0 OR C1 > 32 THEN NEXT
572 FOR R1 = R-JR TO R+JR : IF R1 < 0 OR R1 > 40 THEN NEXT
573 IF NN% = 24 THEN 588
574 REM TOO CLOSE/FAR?
575 GOSUB 558: IF (D% > JR) GOTO 588
576 REM EMPTY HEX?
577 IF SY$(C1,R1) = "" GOTO 588
578 SY$ = SY$(C1,R1):GOSUB 520:REM PARSE SY$
579 X$ = MID$(STR$(C1),2)
580 Y$ = MID$(STR$(R1),2)
581 IF C1 < 10 THEN X$ = "0" + X$
582 IF R1 < 10 THEN Y$ = "0" + Y$
583 NN% = NN% + 1
584 NA$=LEFT$(NA$+"                    ",12)
585 TC$=LEFT$(TC$+"                    ",12)
586 D$=MID$(STR$(D%),2)
587 MA$(NN%) = X$+Y$+" "+D$+" "+NA$+" "+TC$
588 NEXT:NEXT
589 SY$ = SY$(C,R): REM CLEAN UP!
590 RETURN
591 REM C1,R1,D%,JR -> C,R,SY$
592 REM IF D%>JR, NO GO
593 IF (D% > JR) THEN RETURN
594 C = C1: R = R1
595 SY$ = SY$(C,R)
596 RETURN
597 REM SHOW CARGO PRICES
598 ?:? "CARGO PRICES PER TON:"
599 FOR CA = 0 TO 10
600 GOSUB 520 :REM UW$, TC$
601 GOSUB 479
602 PR$ = LEFT$(MID$(STR$(PR)+"                    ",2),5)
603 ? CA, MID$(CT$(CA),4,12) "CR " PR$ " (" CA(CA) "T)"
604 CP(CA) = PR
605 NEXT
606 RETURN
